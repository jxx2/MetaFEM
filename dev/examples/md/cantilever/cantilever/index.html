<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Linear elastic cantilever bending · MetaFEM.jl</title><script data-outdated-warner src="../../../../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.039/juliamono-regular.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.3/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.13.11/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../../../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../../../assets/documenter.js"></script><script src="../../../../siteinfo.js"></script><script src="../../../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><a class="docs-logo" href="../../../../"><img src="../../../../assets/logo.png" alt="MetaFEM.jl logo"/></a><div class="docs-package-name"><span class="docs-autofit"><a href="../../../../">MetaFEM.jl</a></span></div><form class="docs-search" action="../../../../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../../../../">Home</a></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../../pikachu/pikachu/">Thermal conduction in a pikachu</a></li><li><a class="tocitem" href="../../thermal_stripe/thermal_stripe/">Various thermal boundary conditions on a 2D stripe</a></li><li class="is-active"><a class="tocitem" href>Linear elastic cantilever bending</a><ul class="internal"><li><a class="tocitem" href="#Geometry"><span>Geometry</span></a></li><li><a class="tocitem" href="#Physics"><span>Physics</span></a></li><li><a class="tocitem" href="#Assembly"><span>Assembly</span></a></li><li><a class="tocitem" href="#Run"><span>Run</span></a></li><li><a class="tocitem" href="#Plot"><span>Plot</span></a></li><li><a class="tocitem" href="#Bare-script"><span>Bare script</span></a></li></ul></li><li><a class="tocitem" href="../../stress_concentration/stress_concentration/">Stress concentration of a hole</a></li><li><a class="tocitem" href="../../cylinderflow/cylinderflow/">Incompressible flow pass a cylinder</a></li><li><a class="tocitem" href="../../lid_driven_cavity/lid_driven_cavity/">Lid-driven cavity</a></li></ul></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>Linear elastic cantilever bending</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>Linear elastic cantilever bending</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/jxx2/MetaFEM.jl/blob/master/docs/src/examples/src/cantilever/cantilever.jl" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="Linear-elastic-cantilever-bending"><a class="docs-heading-anchor" href="#Linear-elastic-cantilever-bending">Linear elastic cantilever bending</a><a id="Linear-elastic-cantilever-bending-1"></a><a class="docs-heading-anchor-permalink" href="#Linear-elastic-cantilever-bending" title="Permalink"></a></h1><p>In this example we simulate a cantilever beam under different loads, as a straightforward check on the elasticity formulation by comparison to the analytical solutions. The source with data/visualization can also be found <a href="https://github.com/jxx2/MetaFEM.jl/tree/main/examples/cantilever">here</a>.</p><p>First, we load the package and declare the domain:</p><pre><code class="language-julia hljs">using MetaFEM
fem_domain = FEM_Domain(dim = 3)</code></pre><h2 id="Geometry"><a class="docs-heading-anchor" href="#Geometry">Geometry</a><a id="Geometry-1"></a><a class="docs-heading-anchor-permalink" href="#Geometry" title="Permalink"></a></h2><p>For cuboid geometry we have helper functions &quot;make_Square&quot;/&quot;make_Brick&quot; for 2D/3D:</p><pre><code class="language-julia hljs">L_box, e_number, LW_ratio = 1., 4, 10
domain_size = (L_box * LW_ratio, L_box, L_box)
element_number = (Int(e_number * LW_ratio / 4), e_number, e_number)
element_shape = :CUBE

vertices, connections = make_Brick(domain_size, element_number, element_shape)
ref_mesh = construct_TotalMesh(vertices, connections)</code></pre><p>Defining boundary can be a little lengthy because we need to find those IDs. Any idea for improvement will be very welcomed.</p><pre><code class="language-julia hljs">@Takeout (vertices, faces) FROM ref_mesh
facet_IDs = get_BoundaryMesh(ref_mesh)
vIDs = faces.vertex_IDs[:, facet_IDs]

x1_mean = vec(sum(vertices.x1[vIDs], dims = 1)) ./ size(faces.vertex_IDs, 1)
x2_mean = vec(sum(vertices.x2[vIDs], dims = 1)) ./ size(faces.vertex_IDs, 1)
x3_mean = vec(sum(vertices.x3[vIDs], dims = 1)) ./ size(faces.vertex_IDs, 1)

err_scale = L_box / e_number * 0.01

facet_IDs_left = facet_IDs[(x1_mean .&lt; err_scale) .&amp; (x1_mean .&gt; (.- err_scale))]
facet_IDs_right = facet_IDs[(x1_mean .&lt; (L_box * LW_ratio .+ err_scale)) .&amp; (x1_mean .&gt; (L_box * LW_ratio.- err_scale))]
facet_IDs_front = facet_IDs[(x2_mean .&lt; err_scale) .&amp; (x2_mean .&gt; (.- err_scale))]
facet_IDs_back = facet_IDs[(x2_mean .&lt; (L_box .+ err_scale)) .&amp; (x2_mean .&gt; (L_box .- err_scale))]
facet_IDs_bottom = facet_IDs[(x3_mean .&lt; err_scale) .&amp; (x3_mean .&gt; (.- err_scale))]
facet_IDs_top = facet_IDs[(x3_mean .&lt; (L_box .+ err_scale)) .&amp; (x3_mean .&gt; (L_box .- err_scale))]

wp_ID = add_WorkPiece(ref_mesh; fem_domain = fem_domain)
fix_bg_ID = add_Boundary(wp_ID, facet_IDs_left; fem_domain = fem_domain) #left fixed
free_bg_ID = add_Boundary(wp_ID, vcat(facet_IDs_front, facet_IDs_bottom, facet_IDs_top); fem_domain = fem_domain) #bottom &amp; right &amp; front free
back_bg_ID = add_Boundary(wp_ID, facet_IDs_back; fem_domain = fem_domain) #back will be loaded
right_bg_ID = add_Boundary(wp_ID, facet_IDs_right; fem_domain = fem_domain) #top will be loaded</code></pre><h2 id="Physics"><a class="docs-heading-anchor" href="#Physics">Physics</a><a id="Physics-1"></a><a class="docs-heading-anchor-permalink" href="#Physics" title="Permalink"></a></h2><p>The mathematical formulation is:</p><p class="math-container">\[\text{variable}\quad d_i,\qquad\text{parameters}\quad E,\nu,d^w_i,\sigma^l_{ij},\tau\]</p><p class="math-container">\[\lambda=\frac{E\nu}{(1+\nu)(1-2\nu)},\qquad\mu=\frac{E}{2(1+\nu)}\]</p><p class="math-container">\[\epsilon_{ij}=\frac{d_{i,j}+d_{j,i}}{2},\qquad\sigma_{ij}=\lambda\delta_{ij}\epsilon_{mm}+2\mu\epsilon_{ij}\]</p><p class="math-container">\[-(d_{i,j},\sigma_{ij})=0,\qquad in\quad\Omega\]</p><p class="math-container">\[(d_i,\sigma^l_{ij}n_j)=0,\qquad on\quad(\partial\Omega)_{load}\]</p><p class="math-container">\[(d_i,\tau(d^w_i-d_i))=0,\qquad on\quad(\partial\Omega)_{fix}\]</p><p>where <span>$d_i,E,\nu,\lambda,\mu,\epsilon_{ij},\sigma_{ij}$</span> are the displacement, Young’s modulus, Poisson ratio, Lame&#39;s first parameter, shear modulus, strain and stress, respectively.</p><p>The code is: (Note, if we set the Poisson ratio ν = 0.0, the spherical part, i.e., λ related terms, will be actaully removed by the simplification.)</p><pre><code class="language-julia hljs">Δx = L_box / e_number
E = 1
ν = 0.001
λ = E * ν / ((1 + ν) * (1 - 2 * ν))
μ = E / (2 * (1 + ν))
τᵇ = 1000 * E / L_box ^ 2

@Sym d
@External_Sym (dʷ, CONTROLPOINT_VAR) (σˡ, CONTROLPOINT_VAR, SYMMETRIC_TENSOR) (σ², CONTROLPOINT_VAR, SYMMETRIC_TENSOR)
@Def ε{i,j} = (d{i;j} + d{j;i}) / 2.
@Def σ{i,j} = λ * δ{i,j} * ε{m,m} + 2. * μ * ε{i,j}
@Def Elastrostatic_Domain = - Bilinear(ε{i,j}, σ{i,j})

@Def begin
    WF_domain = Elastrostatic_Domain
    WF_fixed_bdy = τᵇ * Bilinear(d{i}, (dʷ{i} - d{i}))

    WF_right_bdy = Bilinear(d{i}, σˡ{i,j} * n{j})
    WF_back_bdy = Bilinear(d{i}, σ²{i,j} * n{j})
end

assign_WorkPiece_WeakForm(wp_ID, WF_domain; fem_domain = fem_domain)
assign_Boundary_WeakForm(wp_ID, fix_bg_ID, WF_fixed_bdy; fem_domain = fem_domain)
assign_Boundary_WeakForm(wp_ID, right_bg_ID, WF_right_bdy; fem_domain = fem_domain)
assign_Boundary_WeakForm(wp_ID, back_bg_ID, WF_back_bdy; fem_domain = fem_domain)</code></pre><h2 id="Assembly"><a class="docs-heading-anchor" href="#Assembly">Assembly</a><a id="Assembly-1"></a><a class="docs-heading-anchor-permalink" href="#Assembly" title="Permalink"></a></h2><pre><code class="language-julia hljs">initialize_LocalAssembly(fem_domain)
mesh_Classical([wp_ID]; shape = element_shape, itp_type = :Serendipity, itp_order = 2, itg_order = 5, fem_domain = fem_domain)
compile_Updater_GPU(; domain_ID = 1, fem_domain = fem_domain)</code></pre><h2 id="Run"><a class="docs-heading-anchor" href="#Run">Run</a><a id="Run-1"></a><a class="docs-heading-anchor-permalink" href="#Run" title="Permalink"></a></h2><p>we run for three different loading conditions:</p><pre><code class="language-julia hljs">for wp in fem_domain.workpieces
    update_Mesh(fem_domain.dim, wp, wp.element_space)
end
assemble_Global_Variables(; fem_domain = fem_domain)
fem_domain.linear_solver = solver_LU_CPU
fem_domain.globalfield.converge_tol = 1e-5

dx = L_box/e_number
h, l = L_box, (L_box * LW_ratio)
I = 1/12 * h ^ 3
err_scale = 0.25

cpts = fem_domain.workpieces[1].mesh.controlpoints
horizontal_mid_cp_IDs = findall((cpts.x2 .&gt; L_box / 2 - err_scale * dx) .&amp; (cpts.x2 .&lt; L_box / 2 + err_scale * dx) .&amp;
                                (cpts.x3 .&gt; L_box / 2 - err_scale * dx) .&amp; (cpts.x3 .&lt; L_box / 2 + err_scale * dx))

x_plot = cpts.x1[horizontal_mid_cp_IDs] |&gt; collect
sorted_ids = sortperm(x_plot)

horizontal_mid_cp_IDs = horizontal_mid_cp_IDs[MetaFEM.cu(sorted_ids)]
x_plot = x_plot[sorted_ids]
y_plots = [[] for i = 1:2]
plot_labels = [String[] for i = 1:2]

σ_external = 1e6
cpts.σˡ6 .= σ_external
cpts.σ²2 .= 0
update_OneStep(fem_domain.time_discretization; fem_domain = fem_domain)
dessemble_X(fem_domain.workpieces, fem_domain.globalfield)

y_plot_ana = σ_external * L_box/(6 * E * I) * (3 * l .-  x_plot) .* x_plot .^ 2
y_plot_num = cpts.d2[horizontal_mid_cp_IDs] |&gt; collect
y_max = maximum(y_plot_ana)

push!(y_plots[1], y_plot_ana ./ y_max)
push!(y_plots[2], y_plot_num ./ y_max)
push!(plot_labels[1], &quot;Concentrated load, analytical&quot;)
push!(plot_labels[2], &quot;Concentrated load, MetaFEM&quot;)

cpts.σˡ6 .= 0
cpts.σ²2 .= σ_external
update_OneStep(fem_domain.time_discretization; fem_domain = fem_domain)
dessemble_X(fem_domain.workpieces, fem_domain.globalfield)

y_plot_ana = σ_external / (24 * E * I) * (x_plot.^2 .+ 6 * l ^ 2 .- 4 * l .* x_plot) .* x_plot .^ 2
y_plot_num = cpts.d2[horizontal_mid_cp_IDs] |&gt; collect
y_max = maximum(y_plot_ana)

push!(y_plots[1], y_plot_ana ./ y_max)
push!(y_plots[2], y_plot_num ./ y_max)
push!(plot_labels[1], &quot;Uniform pressure, analytical&quot;)
push!(plot_labels[2], &quot;Uniform pressure, MetaFEM&quot;)

cpts.σ²2 .= σ_external .* (1. .- cpts.x1 ./ (L_box * LW_ratio))
update_OneStep(fem_domain.time_discretization; fem_domain = fem_domain)
dessemble_X(fem_domain.workpieces, fem_domain.globalfield)

y_plot_ana = σ_external / (120 * l * E * I) * (10 * l ^ 3 .- 10 * l^2 .* x_plot .+ 5 * l * x_plot .^ 2 .- x_plot .^ 3) .* x_plot .^ 2
y_plot_num = cpts.d2[horizontal_mid_cp_IDs] |&gt; collect
y_max = maximum(y_plot_ana)

push!(y_plots[1], y_plot_ana ./ y_max)
push!(y_plots[2], y_plot_num ./ y_max)
push!(plot_labels[1], &quot;Linearly distributed pressure, analytical&quot;)
push!(plot_labels[2], &quot;Linearly distributed pressure, MetaFEM&quot;)</code></pre><h2 id="Plot"><a class="docs-heading-anchor" href="#Plot">Plot</a><a id="Plot-1"></a><a class="docs-heading-anchor-permalink" href="#Plot" title="Permalink"></a></h2><p>With Makie.jl:</p><pre><code class="language-julia hljs">using CairoMakie, Colors

fig = Figure(resolution = (1400, 900))
ax1 = fig[1, 1] = Axis(fig, title = &quot;Normalized deflection on the line y = z = 0.5&quot;,
                    xlims = (0., 10.0), xticks = 0:2:10, ylims = (0, 1), yticks = 0:0.2:1, xlabel = &quot;x&quot;, ylabel = &quot;Normalized d₂&quot;)
fontsize = 24
ax1.titlesize = fontsize
ax1.xlabelsize = fontsize
ax1.ylabelsize = fontsize

plots = [[] for i = 1:2]

ana_plot = scatter!(ax1, x_plot, y_plots[1][1], marker = &#39;■&#39;, markersize = 10px, color = :blue)
num_plot = scatterlines!(ax1, x_plot, y_plots[2][1], marker = :circle, markersize = 5px, color = :red, markercolor = :red)
push!(plots[1], ana_plot)
push!(plots[2], num_plot)

ana_plot = scatter!(ax1, x_plot, y_plots[1][2], marker = &#39;■&#39;, markersize = 10px, color = :gray)
num_plot = scatterlines!(ax1, x_plot, y_plots[2][2], marker = :circle, markersize = 5px, color = :brown, markercolor = :brown)
push!(plots[1], ana_plot)
push!(plots[2], num_plot)

ana_plot = scatter!(ax1, x_plot, y_plots[1][3], marker = &#39;■&#39;, markersize = 10px, color = :green)
num_plot = scatterlines!(ax1, x_plot, y_plots[2][3], marker = :circle, markersize = 5px, color = :purple, markercolor = :purple)
push!(plots[1], ana_plot)
push!(plots[2], num_plot)

Legend(fig, vcat(plots...), vcat(plot_labels...), bbox = (300, 500, 600, 800), labelsize = fontsize)
fig</code></pre><p><img src="../cantilever.png" alt="cantilever"/></p><h2 id="Bare-script"><a class="docs-heading-anchor" href="#Bare-script">Bare script</a><a id="Bare-script-1"></a><a class="docs-heading-anchor-permalink" href="#Bare-script" title="Permalink"></a></h2><pre><code class="language-julia hljs">using MetaFEM
fem_domain = FEM_Domain(dim = 3)

L_box, e_number, LW_ratio = 1., 4, 10
domain_size = (L_box * LW_ratio, L_box, L_box)
element_number = (Int(e_number * LW_ratio / 4), e_number, e_number)
element_shape = :CUBE

vertices, connections = make_Brick(domain_size, element_number, element_shape)
ref_mesh = construct_TotalMesh(vertices, connections)

@Takeout (vertices, faces) FROM ref_mesh
facet_IDs = get_BoundaryMesh(ref_mesh)
vIDs = faces.vertex_IDs[:, facet_IDs]

x1_mean = vec(sum(vertices.x1[vIDs], dims = 1)) ./ size(faces.vertex_IDs, 1)
x2_mean = vec(sum(vertices.x2[vIDs], dims = 1)) ./ size(faces.vertex_IDs, 1)
x3_mean = vec(sum(vertices.x3[vIDs], dims = 1)) ./ size(faces.vertex_IDs, 1)

err_scale = L_box / e_number * 0.01

facet_IDs_left = facet_IDs[(x1_mean .&lt; err_scale) .&amp; (x1_mean .&gt; (.- err_scale))]
facet_IDs_right = facet_IDs[(x1_mean .&lt; (L_box * LW_ratio .+ err_scale)) .&amp; (x1_mean .&gt; (L_box * LW_ratio.- err_scale))]
facet_IDs_front = facet_IDs[(x2_mean .&lt; err_scale) .&amp; (x2_mean .&gt; (.- err_scale))]
facet_IDs_back = facet_IDs[(x2_mean .&lt; (L_box .+ err_scale)) .&amp; (x2_mean .&gt; (L_box .- err_scale))]
facet_IDs_bottom = facet_IDs[(x3_mean .&lt; err_scale) .&amp; (x3_mean .&gt; (.- err_scale))]
facet_IDs_top = facet_IDs[(x3_mean .&lt; (L_box .+ err_scale)) .&amp; (x3_mean .&gt; (L_box .- err_scale))]

wp_ID = add_WorkPiece(ref_mesh; fem_domain = fem_domain)
fix_bg_ID = add_Boundary(wp_ID, facet_IDs_left; fem_domain = fem_domain) #left fixed
free_bg_ID = add_Boundary(wp_ID, vcat(facet_IDs_front, facet_IDs_bottom, facet_IDs_top); fem_domain = fem_domain) #bottom &amp; right &amp; front free
back_bg_ID = add_Boundary(wp_ID, facet_IDs_back; fem_domain = fem_domain) #back will be loaded
right_bg_ID = add_Boundary(wp_ID, facet_IDs_right; fem_domain = fem_domain) #top will be loaded

Δx = L_box / e_number
E = 1
ν = 0.001
λ = E * ν / ((1 + ν) * (1 - 2 * ν))
μ = E / (2 * (1 + ν))
τᵇ = 1000 * E / L_box ^ 2

@Sym d
@External_Sym (dʷ, CONTROLPOINT_VAR) (σˡ, CONTROLPOINT_VAR, SYMMETRIC_TENSOR) (σ², CONTROLPOINT_VAR, SYMMETRIC_TENSOR)
@Def ε{i,j} = (d{i;j} + d{j;i}) / 2.
@Def σ{i,j} = λ * δ{i,j} * ε{m,m} + 2. * μ * ε{i,j}
@Def Elastrostatic_Domain = - Bilinear(ε{i,j}, σ{i,j})

@Def begin
    WF_domain = Elastrostatic_Domain
    WF_fixed_bdy = τᵇ * Bilinear(d{i}, (dʷ{i} - d{i}))

    WF_right_bdy = Bilinear(d{i}, σˡ{i,j} * n{j})
    WF_back_bdy = Bilinear(d{i}, σ²{i,j} * n{j})
end

assign_WorkPiece_WeakForm(wp_ID, WF_domain; fem_domain = fem_domain)
assign_Boundary_WeakForm(wp_ID, fix_bg_ID, WF_fixed_bdy; fem_domain = fem_domain)
assign_Boundary_WeakForm(wp_ID, right_bg_ID, WF_right_bdy; fem_domain = fem_domain)
assign_Boundary_WeakForm(wp_ID, back_bg_ID, WF_back_bdy; fem_domain = fem_domain)

initialize_LocalAssembly(fem_domain)
mesh_Classical([wp_ID]; shape = element_shape, itp_type = :Serendipity, itp_order = 2, itg_order = 5, fem_domain = fem_domain)
compile_Updater_GPU(; domain_ID = 1, fem_domain = fem_domain)

for wp in fem_domain.workpieces
    update_Mesh(fem_domain.dim, wp, wp.element_space)
end
assemble_Global_Variables(; fem_domain = fem_domain)
fem_domain.linear_solver = solver_LU_CPU
fem_domain.globalfield.converge_tol = 1e-5

dx = L_box/e_number
h, l = L_box, (L_box * LW_ratio)
I = 1/12 * h ^ 3
err_scale = 0.25

cpts = fem_domain.workpieces[1].mesh.controlpoints
horizontal_mid_cp_IDs = findall((cpts.x2 .&gt; L_box / 2 - err_scale * dx) .&amp; (cpts.x2 .&lt; L_box / 2 + err_scale * dx) .&amp;
                                (cpts.x3 .&gt; L_box / 2 - err_scale * dx) .&amp; (cpts.x3 .&lt; L_box / 2 + err_scale * dx))

x_plot = cpts.x1[horizontal_mid_cp_IDs] |&gt; collect
sorted_ids = sortperm(x_plot)

horizontal_mid_cp_IDs = horizontal_mid_cp_IDs[MetaFEM.cu(sorted_ids)]
x_plot = x_plot[sorted_ids]
y_plots = [[] for i = 1:2]
plot_labels = [String[] for i = 1:2]

σ_external = 1e6
cpts.σˡ6 .= σ_external
cpts.σ²2 .= 0
update_OneStep(fem_domain.time_discretization; fem_domain = fem_domain)
dessemble_X(fem_domain.workpieces, fem_domain.globalfield)

y_plot_ana = σ_external * L_box/(6 * E * I) * (3 * l .-  x_plot) .* x_plot .^ 2
y_plot_num = cpts.d2[horizontal_mid_cp_IDs] |&gt; collect
y_max = maximum(y_plot_ana)

push!(y_plots[1], y_plot_ana ./ y_max)
push!(y_plots[2], y_plot_num ./ y_max)
push!(plot_labels[1], &quot;Concentrated load, analytical&quot;)
push!(plot_labels[2], &quot;Concentrated load, MetaFEM&quot;)

cpts.σˡ6 .= 0
cpts.σ²2 .= σ_external
update_OneStep(fem_domain.time_discretization; fem_domain = fem_domain)
dessemble_X(fem_domain.workpieces, fem_domain.globalfield)

y_plot_ana = σ_external / (24 * E * I) * (x_plot.^2 .+ 6 * l ^ 2 .- 4 * l .* x_plot) .* x_plot .^ 2
y_plot_num = cpts.d2[horizontal_mid_cp_IDs] |&gt; collect
y_max = maximum(y_plot_ana)

push!(y_plots[1], y_plot_ana ./ y_max)
push!(y_plots[2], y_plot_num ./ y_max)
push!(plot_labels[1], &quot;Uniform pressure, analytical&quot;)
push!(plot_labels[2], &quot;Uniform pressure, MetaFEM&quot;)

cpts.σ²2 .= σ_external .* (1. .- cpts.x1 ./ (L_box * LW_ratio))
update_OneStep(fem_domain.time_discretization; fem_domain = fem_domain)
dessemble_X(fem_domain.workpieces, fem_domain.globalfield)

y_plot_ana = σ_external / (120 * l * E * I) * (10 * l ^ 3 .- 10 * l^2 .* x_plot .+ 5 * l * x_plot .^ 2 .- x_plot .^ 3) .* x_plot .^ 2
y_plot_num = cpts.d2[horizontal_mid_cp_IDs] |&gt; collect
y_max = maximum(y_plot_ana)

push!(y_plots[1], y_plot_ana ./ y_max)
push!(y_plots[2], y_plot_num ./ y_max)
push!(plot_labels[1], &quot;Linearly distributed pressure, analytical&quot;)
push!(plot_labels[2], &quot;Linearly distributed pressure, MetaFEM&quot;)

using CairoMakie, Colors

fig = Figure(resolution = (1400, 900))
ax1 = fig[1, 1] = Axis(fig, title = &quot;Normalized deflection on the line y = z = 0.5&quot;,
                    xlims = (0., 10.0), xticks = 0:2:10, ylims = (0, 1), yticks = 0:0.2:1, xlabel = &quot;x&quot;, ylabel = &quot;Normalized d₂&quot;)
fontsize = 24
ax1.titlesize = fontsize
ax1.xlabelsize = fontsize
ax1.ylabelsize = fontsize

plots = [[] for i = 1:2]

ana_plot = scatter!(ax1, x_plot, y_plots[1][1], marker = &#39;■&#39;, markersize = 10px, color = :blue)
num_plot = scatterlines!(ax1, x_plot, y_plots[2][1], marker = :circle, markersize = 5px, color = :red, markercolor = :red)
push!(plots[1], ana_plot)
push!(plots[2], num_plot)

ana_plot = scatter!(ax1, x_plot, y_plots[1][2], marker = &#39;■&#39;, markersize = 10px, color = :gray)
num_plot = scatterlines!(ax1, x_plot, y_plots[2][2], marker = :circle, markersize = 5px, color = :brown, markercolor = :brown)
push!(plots[1], ana_plot)
push!(plots[2], num_plot)

ana_plot = scatter!(ax1, x_plot, y_plots[1][3], marker = &#39;■&#39;, markersize = 10px, color = :green)
num_plot = scatterlines!(ax1, x_plot, y_plots[2][3], marker = :circle, markersize = 5px, color = :purple, markercolor = :purple)
push!(plots[1], ana_plot)
push!(plots[2], num_plot)

Legend(fig, vcat(plots...), vcat(plot_labels...), bbox = (300, 500, 600, 800), labelsize = fontsize)
fig

# This file was generated using Literate.jl, https://github.com/fredrikekre/Literate.jl</code></pre><hr/><p><em>This page was generated using <a href="https://github.com/fredrikekre/Literate.jl">Literate.jl</a>.</em></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../../thermal_stripe/thermal_stripe/">« Various thermal boundary conditions on a 2D stripe</a><a class="docs-footer-nextpage" href="../../stress_concentration/stress_concentration/">Stress concentration of a hole »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 0.27.10 on <span class="colophon-date" title="Friday 5 November 2021 16:20">Friday 5 November 2021</span>. Using Julia version 1.6.3.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
